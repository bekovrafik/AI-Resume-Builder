ext {
    compileSdkVersion = 35
    targetSdkVersion = 35
    minSdkVersion = 23
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

subprojects {
    if (project.name != "app") {
        afterEvaluate { project ->
            if (project.hasProperty("android")) {
                project.android {
                    if (namespace == null) {
                        namespace = project.group
                    }
                    compileSdkVersion 35
                    
                    // Enforce 16KB alignment for plugins with native code using official flags
                    defaultConfig {
                        externalNativeBuild {
                            cmake {
                                arguments "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
                            }
                            ndkBuild {
                                arguments "APP_SUPPORT_FLEXIBLE_PAGE_SIZES:=true",
                                          "APP_LDFLAGS+=-Wl,-z,max-page-size=16384"
                            }
                        }
                    }
                    // Enforce uncompressed native libs for plugins
                    packaging {
                        jniLibs {
                            useLegacyPackaging = false
                        }
                    }
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
